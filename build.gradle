plugins {
    id 'net.neoforged.moddev.legacyforge' version '2.0.124'
    id "maven-publish"
    id "com.dorongold.task-tree" version "2.1.1"
}

base {
    archivesName = project.archives_base_name
}

String buildNumber = System.getenv("GITHUB_ACTION_NUMBER")
version = "${mod_version}" + (buildNumber != null ? "-build_${System.getenv("GITHUB_RUN_NUMBER")}" : "")
group = project.maven_group

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}
sourceSets.main {
    ext["refMap"] = "gtlendless.mixins.refmap.json"
}
mixin {
    add sourceSets.main, 'gtlendless.mixins.refmap.json'
    config "gtlendless.mixins.json"
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name = "Forge Maven"
        url = "https://maven.minecraftforge.net/"
    }

    maven {
        name = 'GTCEu Maven'
        url = 'https://maven.gtceu.com'
        content {
            includeGroup 'com.gregtechceu.gtceu'
        }
    }

    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "CurseMaven"
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name "firstdarkdev-low-drag-lib"
        url "https://maven.firstdark.dev/snapshots"
    }
    maven {
        name 'Configuration'
        url 'https://repo.repsy.io/mvn/toma/public/'
    }
    maven {
        name 'Configuration (alternative/backup)'
        url 'https://api.repsy.io/mvn/toma/public/'
    }
    maven { // Registrate
        url "https://maven.tterrag.com/"
    }
}
legacyForge {
    version = "1.20.1-47.4.13"
    runs {
        client {
            client()
            programArguments = [
                    "-mixin.config=gtlendless.mixins.json",
                    "--width",
                    "2560",
                    "--height",
                    "1440"
            ]
            gameDirectory = project.file('run')
        }
    }
    mods {
        gtlendless {

            sourceSet sourceSets.main

        }
    }
    parchment {
        // Get versions from https://parchmentmc.org/docs/getting-started
        // Omit the "v"-prefix in mappingsVersion
        minecraftVersion = "1.20.1"
        mappingsVersion = "2023.09.03"
    }
}
def modrinthMods = [
]
def curseMods = [
        "appliedenergistics2-forge-223794:7148487",
        "guideme-forge-1173950:7127447",
        "jade-1.20.1-324717:6855440",
        "ModernUI-Forge-352491:6956345"
]
dependencies {
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

    modImplementation ("software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}")
    jarJar("software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}") {
        version {
            strictly geckolib_version
        }
    }
    // https://mvnrepository.com/artifact/com.github.ben-manes.caffeine/caffeine
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.3'
    // GTCEu
    modImplementation "com.gregtechceu.gtceu:gtceu-${minecraft_version}:${gtceu_version}"
    modImplementation "dev.toma.configuration:configuration-${minecraft_version}:${configuration_version}-forge"

    modImplementation "com.lowdragmc.ldlib:ldlib-forge-${minecraft_version}:${ldlib_version}"
    // MC<minecraft_version>-<registrate_version>
    modImplementation "com.tterrag.registrate:Registrate:MC1.20-1.3.11"
    // [MC<minecraft_version>,MC<next_minecraft_version>)
    jarJar(group: 'com.tterrag.registrate', name: 'Registrate', version: "MC1.20-1.3.11")


    modrinthMods.each { dep ->
        modImplementation("maven.modrinth:$dep")
    }
    curseMods.each { dep ->
        modImplementation "curse.maven:$dep-deobf"
        //exportModJarsConfig "curse.maven:$dep"
    }

    // JEI
    // at runtime, use the full JEI jar for Forge
    compileOnly("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
    modImplementation("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")


    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

processResources {
    var properties = [
            "mod_license"      : mod_license,
            "mod_id"           : mod_id,
            "version"          : version,
            "mod_name"         : mod_name,
            "mod_url"          : mod_url,
            "mod_author"       : mod_authors,
            "forge_version"    : forge_version,
            "minecraft_version": minecraft_version,
            "gtceu_version"    : gtceu_version,
            "kubejs_version"   : kubejs_version,
            "jei_version"      : jei_version,
    ]
    inputs.properties(properties)

    filesMatching("META-INF/mods.toml") {
        expand properties
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(17)

    options.compilerArgs << "-Xlint:-deprecation"
    options.compilerArgs << "-Xlint:-removal"
}

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        // 发布仓库配置
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(GroovyCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}